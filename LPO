#!/usr/bin/perl -w

use warnings;
use strict;
use v5.10;
use Getopt::Long;

my $aux;        # Auxiliar variable
my $d;          # Debugger Option
my $cnf;        # CNF option
my $lines;      # Line catcher of the INPUT
my @entries;    # Array with the lines
my $file;       # string with the output file
my $usage;      # string with the usage of the program

$usage = "\n    usage: ./LPO INPUT_FILE [-d|--debug] [-c|--cnf] [-o|--output = OUTPUT_FILE]\n";

#Die if there is no arguments
die "I need a file!$usage" if @ARGV == 0;

open(INPUT, "<", $aux = shift @ARGV) or die "Impossible to open \"$aux\"$usage";

GetOptions("debug"    => \$d,
           "cnf"      => \$cnf,
           "output=s" => \$file
);

unless($d)
{ 
    $file = "output.txt" unless $file;
    open(OUT, '>', $file);
    say "The output is in the file \"$file\"";
    select OUT;
}

# Puting the INPUT in @entries
while ($lines = <INPUT>)
{push (@entries, $lines);}

close(INPUT);

say "\n----------------DEBBUGER----------------" if $d;
say "\nINPUT:\n" if $d;

say @entries if $d;

say "\nREADING FILE:\n" if $d;

my %vars;           # Hash with the information of the variables
my %preds;          # Hash with the predicatives variations
my $claus;          # String with the clause
my @conds;          # Array with the conditions for the last clause
my $i;              # Actual line of the INPUT
my $counter = 0;    # line counter 

# Processing loop
while ($i = shift @entries)
{
    $counter++;
    # Check if the actual line is a variable declaration
    if ($i =~ m/([A-Z]+)\s*:\s*(\d+)\s+(\d+)/)
    {
        say $1 . " = {" . $2 . " ... " . $3 . "}" if $d;
        $vars{$1} = [$2, $3, "x"]; #The 'x' is a indicator used after
    }
    else 
    {   
        # Check if the actual line is a clause
        if ($i =~ 
           m/
                (
                    [-]?[a-z]+      # Name of the predicative

                    [(]
                    ([A-Z]+)\s*     # First parameter
                    (,\s*[A-Z]+)*   # Others parameters
                    [)]
                )+                  # One or more predicatives
                
                \s*[.]\s*           # End Of Predicatives
                
                (                   # Conditions
                    (
                        ([A-Z]+|\d+)\s*     # Variable or number 
                        (
                            [+-]\s*         # Operation applied
                            ([A-Z]+|\d+)\s* # Variable or number
                        )*
                                            
                        ([=<>]|!=)\s*   # Condition
                        
                        ([A-Z]+|\d+)\s*     # Variable or number 
                        (
                            [+-]\s*         # Operation applied
                            ([A-Z]+|\d+)\s* # Variable or number
                        )*
                    )+                    
                    [.]
                )?$
            /x
        )
        {   
            # Catching the clauses
            while ($i =~ s/([-]?\w+[(][A-Z,\s]+[)])\s*//)
            {
                $claus .= $1 . " ";
            }
            say "\nclausule:" if $d;
            print "    " if $d;
            say $claus if $d;
            
            #Catching the conditions
            say "conditions: " if $d;
            while ($i =~ s/
                            ([A-Z]+|\d+)     # First term of the condition
                            (
                                \s*
                                [+-]         # Operation applied to the term
                                \s*
                                ([A-Z]+|\d+) # Operator applied to the term
                            )*               # Any number of operations
                            \s*
                            [=<>!]+          # Comparison Operator
                            \s*
                            ([A-Z]+|\d+)     # Second term of the condition
                            (
                                \s*
                                [+-]         # Operation applied to the term
                                \s*
                                ([A-Z]+|\d+) # Operator applied to the term
                            )*               # Any number of operations
                        //x)
            {
                print "    " if $d;
                say $& if $d;
                push(@conds, $&);
                
                #Checking if all Variables in conditions are in the clause
                $aux = $conds[-1];
                while($aux =~ s/([A-Z]+)//)
                {   
                    die "Fatal error: $1 is not in the clause in line $counter\n"
                        unless ($claus =~ m/$1/);
                } 
            }
            
            # Here we start to recursively print
            say "\nPRINTING:\n" if $d;
            $claus =~ m/([A-Z]+)/; # Catch the first variable 
            # Die if the variable wasn't declared
            die "Variable $1 wasn't declared" unless defined $vars{$1}; 
            # The recursive function
            insert(
                    $claus,   # String with the clause 
                    $1,       # Name of one of the variables
                    {%vars},  # Reference to the variable hash
                    [@conds], # Reference to the array of conditions
                    {%preds}  # Reference to the predicatives hash 
                  );
            say "" if $d;       
        }
        #die if the actual line is either not a clause nor a declaration
        else
        {
            die "Error in line $counter: it is neither a declaration nor a clause\n"
                unless $i =~ m/^[\s]*$/;
        }
    }
    #empty the conditions and the clause
    @conds = ();
    $claus = "";
}

# Recursive function to print the clauses
sub insert
{
    my $clause = shift;     # The clauses with some variable
    my $var    = shift;     # A variable in the clause
    my $vHash  = shift;     # A Hash reference with all the variables
    my $checks = shift;     # A Array reference with the conditions
    # A Hash reference with all the predicatives so far
    my $preds  = shift;     
    my $min    = $$vHash{$var}->[0]; # Least value of the $var domain
    my $max    = $$vHash{$var}->[1]; # Greater value of the $var domain
    my $aux;                # Auxiliar variable
    
    #die if the variable was not declared
    die "Variable $var wasn't declared" unless defined $vHash->{$var};
    
    # Printing the clauses
    for(my $i = $min; $i <= $max; $i++)
    {
        $$vHash{$var}[2] = $i; #Store the actual value of the variable
        
        # Check if matches the conditions 
        # or if there is no enough information
        if(chkConditions($vHash, $checks))
        {   
            $aux = $clause;
            # Change the variable to its actual value
            $aux =~ s/$var/$i/g;
            
            # Call insert if still variables to cumpute
            if($aux =~ m/([A-Z]+)/)
            {
                insert($aux, $1, $vHash, $checks);
            }
            else 
            {
                print "to file -> " if $d;
                $aux =~ s/\s*$/./; # The dot after the clause
                # Put the computed clause
                say $aux            unless $cnf;
                cnf($aux,$preds)    if     $cnf
            }
        }   
    }
    #clean the slot with the actual value of the variable
    $$vHash{$var}[2] = 'x';
}

# Pre-processing for sub chk 
sub chkConditions
{
    my $vHash  = shift;     # Hash reference with the variables info
    my $checks = shift;     # Array reference with the conditions
    
    foreach(@$checks)
    {
        when(/(.*)!=(.*)/)  # When it is the different condition
        {
            return 0 if(!chk($1, $2, $vHash, "!"));
        }
        when(/(.*)<(.*)/)   # When it is the less condition
        {
            return 0 if(!chk($1, $2, $vHash, "<"));
        }
        when(/(.*)>(.*)/)   # When it is the greater condition
        {
            return 0 if(!chk($1, $2, $vHash, ">"));
        }
        when(/(.*)=(.*)/)   # When it is the equal condition
        {
            return 0 if(!chk($1, $2, $vHash, "="));
        }
    }
    # If matches the condition, return true     
    return 1;
}

# Check if the condition holds
sub chk
{
    my $left   = shift; # The left expression of the condition
    my $right  = shift; # The right expression of the condition
    my $vHash  = shift; # Hash with the variables info
    my $op     = shift; # String with the condition operator
    my $first = 0;      # Variable to hold the first term
    my $second = 0;     # Variable to hold the second term
    
    # Number witch holds if it is a minus or a plus operation
    my $plus = 1;        
    
    # Calculate the left expression
    while($left =~ s/([A-Z]+|\d+)//)
    {
        if(defined $$vHash{$1})
        {
            #if there is no enough information returns true
            return 1 if $$vHash{$1}[2] eq 'x';
            #sums the variable value into the expression
            $first += $$vHash{$1}[2]*$plus;
        }
        else
        {
            # It must be a number to sum into the expression
            # because it is not in hash
            $first += $1*$plus;
        }
        # After compute the first number, check if there is another term
        if($left =~ s/([+-])//)
        {
            $plus = +1 if $1 eq '+';
            $plus = -1 if $1 eq '-';
        }
    }
    
    $plus = 1;
    
    # Calculate the right expression (Analogue to the left)
    while($right =~ s/([A-Z]+|\d+)//)
    {
        if(defined $$vHash{$1})
        {
            return 1 if $$vHash{$1}[2] eq 'x';
            $second += $$vHash{$1}[2]*$plus;
        }
        else
        {
            $second += $1*$plus;
        }
        if($right =~ s/([+-])//)
        {
            $plus = +1 if $1 eq '+';
            $plus = -1 if $1 eq '-';
        }
    }
    
    # Do the actual check
    given($op)
    {
        when("=")
        {
            return $first == $second;
        }
        when("<")
        {
            return $first <  $second;
        }
        when(">")
        {
            return $first >  $second;
        }
        when("!")
        {
            return $first != $second;
        }
    }
}

sub cnf
{
    my $clause = shift;
    my $preds  = shift;
    while($clause =~ s/([+-]?[a-z]+[(].*[)])//)
    {
        print "$1 ";
    }
    say ".";
}
